apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'quality.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.aaschmid:gradle-cpd-plugin:1.0'
    }
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
    maven {
        url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2'
    }
}
android {
    compileSdkVersion 26
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "lt.blaster.template"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release
    }
    Properties properties = new Properties()
    File propertiesFile = project.rootProject.file('signing.properties')
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
        if (properties.containsKey('STORE_FILE')
                && properties.containsKey('STORE_PASSWORD')
                && properties.containsKey('KEY_ALIAS')
                && properties.containsKey('KEY_PASSWORD')) {
            signingConfigs.release.storeFile = file(properties['STORE_FILE'])
            signingConfigs.release.storePassword = properties['STORE_PASSWORD']
            signingConfigs.release.keyAlias = properties['KEY_ALIAS']
            signingConfigs.release.keyPassword = properties['KEY_PASSWORD']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'InvalidPackage', 'PrivateResource', 'MissingTranslation'
        abortOnError false
    }
}

dependencies {
    ext.support_version = "26.1.0"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$support_version"

    compile 'com.jakewharton.timber:timber:4.6.0'

    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true
    }
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    debugCompile 'com.facebook.stetho:stetho-timber:1.4.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'org.mockito:mockito-core:2.11.0'
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile "io.reactivex.rxjava2:rxjava:2.1.6"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"
}
